table: alttopics (^name)
  ^createfact(^name isAltTopic blabla)
  DATA:
  ~aide
  ~quietesvous
  ~blague
  ~version

outputmacro: ^reprenons()
  \n
  \n
  Reprenons si vous le voulez bien.
  \n
  ^poptopic()


topic: ~aide nostay repeat []
u: (< [aide help "aidez-moi" "pouvez-vous me aider" "peux-tu me aider" "je ai besoin de aide"] >) ^keep()
  Je suis un programme actuellement en développement, ne soyez donc pas trop sévère avec moi, je m'améliore de jour en jour ! \n
  Je suis pour l'instant capable de répondre à vos questions au sujet du crédit immobilier, plus précisément je peux vous parler de : \n
  • L'objet d'un prêt \n
  • L'éligibilité au prêt \n
  • Garanties légales \n
  • Contestation sur la clause de domiciliation des revenus \n
  • Mise à disposition des fonds \n
  • Retour des offres /  complétude avant MAD \n
  • Pratiques dérogatoires \n
  \n
  ^poptopic()


topic: ~quietesvous nostay repeat []
u: (qui * [es êtes est etes] [tu vous]) ^keep()
  Je m'appelle Laure et je suis un robot. Je suis là pour répondre à vos questions. \n
  Et oui, les robots rêvent de moutons électriques. 
  ^reprenons()


topic: ~blague repeat []
u: (blague) Bien sûr ! Que disent deux chats quand ils sont amoureux ?
  a: (félin) Vous la connaissiez.
    ^reprenons()
  a: () "On est félin pour l'autre !"
    ^reprenons()
u: (blague)
  Comment appelle-t-on un chat qui conduit des trains ? \n
  Un cheminou !
  ^reprenons()
u: (blague)
  Un jour une feuille tomba dans l'eau et cria "au secours, j'ai papier !"
  ^reprenons()
u: (blague)
  Comment appelle-t-on un cadeau qui s'en va ? \n
  Une surprise party !
  ^reprenons()
u: (blague) ^keep()
  Je ne connais pas d'autres blagues et nous avons encore quelques points importants à voir.
  ^reprenons()


topic: ~version nostay repeat []
u: (version) ^keep()
  \n \n \n
  Version : 30/03
  \n \n \n
  ^poptopic()
