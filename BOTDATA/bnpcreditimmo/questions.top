outputmacro: ^derog(^n ^d ^r)
	En savoir plus :
	if(^n==1) { ^btn(reply ^"Niveau de décision") }
	if(^d==1) { ^btn(reply ^"Diffusion de l'accord") }
	if(^r==1) { ^btn(reply ^"Régularisation") }

outputmacro: ^garanties()
	$_lib = ^"prêteur de deniers"
	^btn(reply hypothèque) ^btnRP($_lib PPD) ^btns(reply caution nantissement)

outputmacro: ^lienSimu()
	$_lien = ^"https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/estimer-sa-capacite-d-emprunt"
	^"%[Estimer votre capacité d'emprunt]($_lien)"

topic: ~questions repeat []
##<<
u: (_*) ^keep()
	if(_0==^"") {
		# debug : message vide
		Ravie de vous revoir ! ^reuse(AIDER)
	}
##>>
t: AIDER () 
	Comment puis-je vous aider ?

t: AUTRE () ^keep()
	\n
	Avez-vous une autre question ?
	a: (< ~non) 
		^gambit(~fin)
	a: (< ~oui >)
		^reuse(ECOUTE)

t: ECOUTE () ^keep()
	Je vous écoute.



########## FI FiNANCEMENT EN COURS PRÊT IMMO ##########

##### Contestation sur la clause de domiciliation des revenus des offres #####

#! Quand puis je signer chez le notaire ?
u: (<<[quand date dates délai délais] [signer signature signatures accord accords] [notaire notaires]>>) ^keep()
	Vous pouvez signer chez le notaire lorsque l'ensemble des conditions suspensives sont levées. Ces conditions sont différentes selon votre projet immobilier et sont reprisent dans votre offre de prêt \( paragrahe "Autres conditions du crédit" \) . \n
	A noter : une condition suspensive est une condition qui, si elle ne se réalise pas, rend le contrat pour lequel elle a été conclue, caduc. \n
	\n
	^gambit(~)

#! En cas de contestation sur la clause de domiciliation des revenus, comment je dois procéder ?
#! Est-ce qu'on peut lever la contestation de la clause de domiciliation des revenus ?
#! Est-ce que je peux supprimer la clause de domiciliation des revenus ?
u: (<<[contestation contester contesté supprimer supprimé] "clause de domiciliation">>) ^keep()
	Vous avez une possibilité de déroger à la clause de domiciliation des revenus : \n
	• directement lors du montage dans l’outil Défi Immo \( taux de bonif DOM dérogé transmis à 0% \) , \n
	• après envoi du dossier à l’APAC, en informant directement l’APAC pour procéder au changement. \n
	^gambit(~)


##### Etape "Mise à disposition des fonds" #####

#! Mise à disposition des fonds 
#! Quel est le délai pour débloquer / verser les fonds ?
#! Quand aura lieu le déblocage des fonds ?
#! A quel moment je dois demander le déblocage des fonds ?
u: (<<[quand moment délai délais mise mises mettre] [déblocage débloquer débloque débloqué débloqués disposition disposer dispositions dispo] [argent sommes fonds]>>) ^keep()
	Les sommes sont disponibles postérieurement à l’acceptation de l’offre, et en fonction de la date de signature de l’acte matérialisant l’opération financée. \n
	Une demande de déblocage des fonds doit parvenir au moins 7 jours avant une signature chez le notaire. \n
	Si vous devez demander un versement avant 7 jours, vous devez vous rapprocher rapidement de votre conseiller. \n
	\n
	^gambit(~)

#! Comment faire pour débloquer les fonds ? 
#! Quels documents pour débloquer les fonds ? 
u: (<<[comment documents document "que faire"] [fonds argent sommes]>>) ^keep()
	Tout d'abord, l'offre de prêt de prêt doit être acceptée et les éventuelles conditions suspensives doivent être levées. Ces dernières sont reprises dans votre offre de prêt \( paragrahe "Autres conditions du crédit" \) . \n
	A noter : une condition suspensive est une condition qui, si elle ne se réalise pas, rend le contrat pour lequel elle a été conclue, caduc.  \n
	\n
	^gambit(~)

#! Quelles sont les modalités de communication des factures ?
#! Est-ce que je dois respecter des consignes / modes opératoires particulières pour me faire rembourser?
#! Comment je dois transmettre les factures pour me faire rembourser ?
u: (<<[modalités transmettre "modes opératoires" consignes] [facture factures rembourser remboursement]>>) ^keep()
	Le règlement ou le remboursement d’une facture est effectué sur la base d’une photocopie de la facture mentionnant l’adresse de réalisation des travaux et sur laquelle il est précisé : \n
	• la mention "Bon à payer à hauteur de .... €" \( montant en chiffres et en lettres \) , \n
	• la date et votre signature. \n
	\n
	Lorsque les fonds sont avancés, les précisions suivantes sont nécessaires : \n
	. la mention "Bon pour remboursement en compte à hauteur de .... €" \( montant en chiffres et en lettres \) . \n
	. le mode de paiement utilisé : "payé par carte bancaire" - "payé par chèque n°... ". \n
	. le justificatif de paiement, en cas de règlement de la facture. \n
	\n
	Vous avez la possibilité de vous faire rembourser une facture réglée en partie avec des avoirs. Si les avoirs apparaissent clairement sur la facture il n’est pas nécessaire de demander un justificatif complémentaire. Ce principe ne peut pas s’appliquer au Bons d’achat. \n
	^gambit(~)

#! Comment vous adresser mes factures ? 
#! Comment vous envoyer mes factures ? 
u: (<<[adresser envoyer courrier transférer] [facture factures]>>) ^keep()
	L'adresse à laquelle les documents doivent être retournés figure en en-tête du courrier d'accompagnement de l'offre de prêt. \n
	\n
	Mettre en 2ème niveau de réponse : \n
	Nous vous rappelons que la demande de versement devra être accompagnée pour chaque facture d'une copie de la facture ou, en cas d'avance des fonds, joindre un justificatif de règlement de la facture (copie de relevé de compte) dont le montant devra être au minimum égal au montant du crédit. \n
	\n
	^gambit(~)

#! Pour un remboursement de factures, comment je dois procéder ?
#! Comment je dois faire pour me faire rembourser une facture ?
u: (<<[rembourser remboursement remboursé remboursés] [factures facture]>>) ^keep()
	Nous procédons au règlement ou un remboursement de factures à l’aide de la demande de versement de fonds jointe à l’offre de prêt. Une demande sur papier libre reprenant les références du prêt peut pallier à la perte du document ou à l’insuffisance d’exemplaires. \n
	$_consignes = ^"Consignes et modalités de facture"
	En savoir plus : btn(reply $_consignes)
	^gambit(~)


##### Etape "Retour des offres / complétude avant MAD" #####

#! Retour des offres / complétude avant MAD
u: ([retour complétude retours complétudes] [avant des] [offres offre MAD]) ^keep()
	Que souhaitez-vous savoir concernant le retour des offres / complétude avant MAD ?

#! Avec une SCI faut il une caution de personne physique ?
u: (<<SCI [caution cautionnement cautions cautionner] "personne physique">>) ^keep()
	Tout à fait il faut au minimum une caution, voire davantage. Le nombre de caution dépend des caractéristiques de votre projet immobilier. Pour connaitre nos solutions de financement, veuillez cliquer sur les liens suivants : \n
	^"%[Prêt Immo Taux fixe](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-immobilier-taux-fixe)" \n
 	^"%[Prêts relais](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-relais)" \n
	^"%[Prêt Relais Acquisition](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-relais-acquisition)" \n

#! Où dois-je retourner mon offre ? 
#! A quel endroit je dois retourner mon offre ?
#! A quelle adresse retourner mon offre de prêt ?
u: (<<[ou où "endroit" lieu adresse] [retour retourner envoyer renvoyer signer signe]>>) ^keep()
	L'adresse à laquelle les documents doivent être retournés figure en en-tête du courrier d'accompagnement de l'offre de prêt. \n
	A noter que cette lettre précise également les documents à retouner. \n
	\n
	$_retour = ^"Retour de mon offre"
	$_delai = ^"Délai de validité de l'offre"
	En savoir plus : ^btns(reply $_retour $_delai) \n
	^gambit(~)

#! Quels sont les documents d'offres à retourner ?
#! Quelles sont les modalités de retour de l'offre de prêt ?
#! Comment je dois retourner les documents ?
#! Quels sont les documents d'offres à signer ?
u: (<<[documents modalités] [retour retourner envoyer renvoyer signer signe]>>) ^keep()
	Tout dépend si vous êtes notamment une personne physique ou une personne morale \( une SCI \)  et de la garantie retenue. \n
	Par exemple, si vous souhaitez acquérir un appartement avec une garantie hypothécaire, vous devez retourner obligatoirement par voie postale a minima les documents suivants : \n
	• L'offre de prêt signée \n
	• Le bordereau d'acceptation signé et daté \n
	• La Fiche standardisée Européenne signée \n
	• La Fiche Explicative signée \n
	\n
	Sachez que toutes les conditions de retour sont précisées conditions précisées dans la lettre d'accompagnement à l'offre de prêt. \n
	\n
	$_retour = ^"Retour de mon offre"
	$_delai = ^"Délai de validité de l'offre"
	En savoir plus : ^btns(reply $_retour $_delai) \n

#! A quel moment je peux retourner l'offre de prêt ?
#! Comment je retourne mon offre ?
#! Quel est le délai de retour ?
#! Puis-je retourner mon offre dès sa réception ?
#! Quand dois-je renvoyer mes offres de pret ?
u: (<<[offre délai prêt offres] [retour retourner retourne renvoi renvoyer]>>) ^keep()
	Tout dépend si vous êtes notamment une personne physique ou une personne morale \( une SCI \) et de la garantie retenue. \n
	Par exemple, si vous souhaitez acquérir un appartement avec une garantie hypothécaire, le délai de réflexion est de 10 jours francs à compter de la réception de l'offre \( cachet de la poste fait foi \) . \n
	L'offre de prêt peut être acceptée à partir du 11ème jour calendaire et doit être retournée obligatoirement par voie postale à l'adresse indiquée dans le courrier d'accompagnement. \n
	\n
	$_doc = ^"Documents à retourner"
	$_delai = ^"Délai de validité de l'offre"
	En savoir plus : ^btns(reply $_doc $_delai) \n

#! Quel est le délai de validité de l'offre de prêt ?
#! Est-ce que les conditions de l'offre sont valables tout le temps ?
#! Délai de validité de mon offre ?
#! Combien de temps mon offre de prêt est elle valable ?
#! Délais de validité de mon offre de prêt ?
u: (!pas <<[durée délai conditions temps délais] offre>>) ^keep()
	L’offre de prêt est valable pendant 30 jours à dater de sa réception par le client. \n
	Après ce délai, BNP Paribas n’est pas tenu de maintenir les conditions. \n
	\n
	$_retour = ^"Retour de mon offre"
	$_doc = ^"Documents à retourner"
	En savoir plus : ^btns(reply $_retour $_doc) \n
	
#! Sous quel délai les fonds sont à utiliser ?
#! A partir de quand je peux demander le versement des fonds ?
#! Quelle est la date limite de déblocage des fonds ?
#! Est-ce que le montant de mon crédit est toujours disponible ?
#! date maximum pour verser les fonds ?
u: (<<[délai délais disponible quand limite maximum] [fonds crédit]>>) ^keep()
	Les fonds doivent être versés dans un délai maximum de 4 mois à compter de l’acceptation de l’offre. \,
	Après ce délai, BNP Paribas n’est pas tenu de maintenir les conditions. Dans certains cas, l’offre de prêt peut être prorogé. \n
	La période pour utiliser les fonds est de 24 mois. \n
	^gambit(~)



########## PRATIQUES DEROGATOIRES ##########

#! Pratiques dérogatoires
u: (<<[pratique pratiques] [dérogation dérogatoire dérogatoires]>>) ^keep()
	Quelle est votre question par rapport aux pratiques dérogatoires? \n
	^reuse(ECOUTE)
#! Quel est le  niveau de décision requis pour monter un dossier de rachat sans l'offre initiale ou l'attestation de prêt ?
u: A1 (<< ["niveau de décision" DCR CSI DR DIA DIE REBP] >> [sans absence déroger dérogation perdu perdue] !avec <<[accord offre] attestation>>) ^keep()
	Le niveau de décision requis est soit le directeur de la DR ou soit le DCR de la DR. \n
	\n
	^derog(0 1 1) \n
	a: (diffusion de le accord) ^reuse(A2)
	a: (régularisation) ^reuse(A3)
#! Comment je dois diffuser un accord à l'APAC en cas de dérogation de l'attestation de prêt ?
u: A2 (<< [diffuser diffusé diffusion IFIP MAESTRO] >> [sans absence déroger dérogation perdu perdue] !avec <<[accord offre] attestation>>) ^keep()
	L’accord doit être diffusé par notification IFIP / MAESTRO \n
	\n
	^derog(1 0 1) \n
	a: (niveau de décision) ^reuse(A1)
	a: (régularisation) ^reuse(A3)
#! Comment je dois régulariser une dossier de rachat sans attestation de prêt ni d'offre de prêt initiale ?
u: A3 (<< [régulariser régularisation régularisé régularisée] >> [sans absence déroger dérogation perdu perdue] !avec <<[accord offre] attestation>>) ^keep()
	La demande doit être régularisée. Elle est à la main du décideur de la DR. Si la régularisation est demandé, celle-ci devra être suivie par l’APAC \n
	\n
	^derog(1 1 0) \n
	a: (niveau de décision) ^reuse(A1)
	a: (diffusion de le accord) ^reuse(A2)
#! Est-ce qu'on peut monter un dossier de rachat sans l'offre initiale ou l'attestation de prêt de la banque ?
#! Quelle est la procédure dans le cas suivant  : en cas d'absence de l'offre d'origine ou d'une attestation de prêt, est-ce que je peux quand même monter un dossier ?
#! Question sur rachat sans offre d'origine ou d'attestation de prêt ?
#! Que devons-nous faire en cas de rachat sans offre iniitiale ou d'attestation de prêt ?
u: ([sans absence déroger dérogation perdu perdue] !avec <<[accord offre] attestation>>) ^keep()
	L’offre peut être éditée en l’absence de l’offre initiale du confrère et en l’absence de l’attestation de la banque émettrice. \n
	\n
	^derog(1 1 1) \n
	a: (niveau de décision) ^reuse(A1)
	a: (diffusion de le accord) ^reuse(A2)
	a: (régularisation) ^reuse(A3)


#! Quel est le niveau de décision requis pour faire accepter une attestation en lieu et place de l'offre de prêt initiale pour un rachat ?
u: B1 (<< ["niveau de décision" DCR CSI DR DIA DIE REBP] >> [sans absence déroger dérogation "au lieu de" "en lieu et place de"] * offre) ^keep()
	Le niveau de décision requis est soit l’équipe de direction, soit le DCR, soit l’expert Crédit ou soit le CSI. \n
	\n
	^derog(0 1 0) \n
	a: (diffusion de le accord) ^reuse(B2)
#! Comment je dois diffuser un accord à l'APAC en cas de dérogation de recueil de l'offre initiale pour un rachat ?
u: B2 (<< [diffuser diffusé diffusion IFIP MAESTRO] >> [sans absence déroger dérogation "au lieu de" "en lieu et place de"] * offre) ^keep()
	L’accord doit être diffusé par notification IFIP / MAESTRO \n
	\n
	^derog(1 0 0) \n
	a: (niveau de décision) ^reuse(B1)
#! Pour un rachat de prêt, est-ce que je peux déroger au recueil de l'offre initiale du confrère avec l'attestation de prêt ?
#! Est-ce que je peux monter un dossier de rachat en l'absence de l'offre initiale ?
#! Quelle est la procédure dans le cas suivant  : en cas d'absence de l'offre d'origine, est-ce que je peux quand même monter un dossier ?
#! Question sur rachat sans offre d'origine ?
#! Ai-je besoin de l'offre d'origine pour un rachat ?
#! J'ai perdu l'offre d'origine du pret
u: ([sans absence déroger dérogation besoin "au lieu de" "en lieu et place de" perdu perdue] * offre) ^keep()
	L’attestation de prêt est nécessaire et doit comporter l’objet du prêt initial, le capital restant dû, la durée restante, le niveau de garanties ainsi que l’identité de l’emprunteur et l’adresse du bien. Cette attestation doit être jointe au dossier transmis à l’APAC. \n
	\n
	^derog(1 1 0) \n
	a: (niveau de décision) ^reuse(B1)
	a: (diffusion de le accord) ^reuse(B2)


u: C1 (<< ["niveau de décision" DCR CSI DR DIA DIE REBP] >> [proroger proroge prorogation prolonger prolonge prolongation] * [validité offre offres]) ^keep()
	Le niveau de décision est différent selon le délai : \n
	• Si le délai est supérieur à 1 an : décision nécessaire de l’équipe de Direction \n
	• Si le délai est entre 6 mois et 1 an \( soit 8 mois de + \) : décision nécessaire soit du DCR, de l’expert crédit, du DIE, du DIE Adjoint ou du REBP \n
	• SI le délai est inférieur ou égal à 6 mois (soit 2 mois de +) : décision nécessaire soit du DIA ou du CSI. \n
	\n
	^derog(0 1 1) \n
	a: (diffusion de le accord) ^reuse(C2)
	a: (régularisation) ^reuse(C3)
#! Comment diffuser l'accord de prorogation de validité de l'offre ?
u: C2 (<< [diffuser diffusé diffusion IFIP MAESTRO] >> [proroger proroge prorogation prolonger prolonge prolongation] * [validité offre offres]) ^keep()
	L’accord doit être diffusé par notification depuis MAESTRO \n
	\n
	^derog(1 0 1) \n
	a: (niveau de décision) ^reuse(C1)
	a: (régularisation) ^reuse(C3)
#! Est-ce que je dois régulariser un accord de prorogation de validité de l'offre ?
u: C3 (<< [régulariser régularisation régularisé régularisée] >> [proroger proroge prorogation prolonger prolonge prolongation] * [validité offre offres]) ^keep()
	La demande doit être régularisée avec la nécessité de recueillir la lettre de demande des emprunteurs avant la mise à disposition des fonds. \n
	\n
	^derog(1 1 0) \n
	a: (niveau de décision) ^reuse(C1)
	a: (diffusion de le accord) ^reuse(C2)
#! Est-ce que le client peut proroger la validité des offres ?
#! Comment faire pour demander de proroger la validité des offres ?
#! Quelle est la procédure à suivre pour proroger l'offre ?
#! Question sur prorogation de l'offre ?
#! Je suis sollicité par un client voulant prolonger la durée de validité des offres, comment faire ?
#! Puis je proroger un pret relais ?
u: PROROG ([proroger proroge prorogation prolonger prolonge prolongation] * [validité offre offres "prêt relais"]) ^keep()
	Vous pouvez demander la prorogation de la validité des offres, au-delà du délai standard fixé à 4 mois. \n
	\n
	^derog(1 1 1) \n
	a: (niveau de décision) ^reuse(C1)
	a: (diffusion de le accord) ^reuse(C2)
	a: (régularisation) ^reuse(C3)
#! Je viens de recevoir une demande du client m'indiquant qu'il ne pourra pas signer son offre dans le délai. Quel moyen à disposition pour répondre à sa demande ?
u: ([peux peut pourrai pourra va] * pas [signer valider] * [délai délais]) ^keep()
	^reuse(PROROG)



########## FI TAUX FIXE IMMOBILIER ##########

##### Objet du prêt #####

#! Peut-on payer la construction de nouveaux bâtiments avec le crédit immobilier ?
#! Puis-je financer des travaux de construction d'un bien immobilier ?
#! Est-il possible de payer mes travaux en cours à mon domicile ?
#! Peut-on financer des travaux de construction d'appartements ?
#! Quels travaux je peux financer ?
#! Je veux faire des travaux
u: (<<[financer financez acheter achat payer faire refaire] [travaux construire construction constructions]>>) ^keep()
	Le crédit immobilier peut en effet financer les travaux de construction d’un bien immobilier à condition qu'il soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	^lienSimu()

#! Est-ce que je peux acheter un terrain avec ce crédit ?
#! Peut-on financer l'achat de nouveaux terrains avec votre crédit immobilier ?
#! Je veux acheter un terrain
u: (<<[financer financez acheter achat payer] [terrain terrains parcelle parcelles]>>) ^keep()
	Oui, à condition que le terrain soit destiné à la construction d’un bien immobilier.
	^lienSimu()

#! Est-ce que je peux acheter des parts de sociétés immobilières ?
#! Peut-on financer des parts de sociétés donnant vocation à l’attribution d’un bien immobilier en propriété ou en jouissance ?
#! Puis-je payer avec ce crédit des parts de sociétés d’attribution ?
u: (<<[financer financez acheter achat payer] "parts de sociétés" [immobilier immobilières attribution]>>) ^keep()
	Il est possible de financer des parts de sociétés immobilières avec un crédit à la consommation ou avec un prêt immobilier. On a recours au prêt à la consommation lorsque le montant de l’achat est inférieur à 75 000 €, et l’on utilise le crédit immobilier lorsqu’il est supérieur à 75 000 €.
	^lienSimu()
u: (<<[financer financez acheter achat payer] SCPI>>) ^keep()
	Il est possible de financer des parts de SCPI avec un crédit à la consommation ou avec un prêt immobilier. On a recours au prêt à la consommation lorsque le montant de l’achat est inférieur à 75 000 €, et l’on utilise le crédit immobilier lorsqu’il est supérieur à 75 000 €.
	^lienSimu()
#! puis acheter ma RP
u: (<<[financer financez acheter achat payer] _~habitatf>>) ^keep()
	Oui, nous finançons l'achat d'une _0, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	^lienSimu()
#! Puis-je acheter des logements avec votre crédit ?
#! Est-il possible de financer des immeubles avec ce crédit immobilier ?
u: (<<[financer financez acheter achat payer] _~habitatp>>) ^keep()
	Oui, nous finançons l'achat de _0, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	^lienSimu()
#! Est-ce que je peux acheter un appartement avec ce prêt ?
#! Je veux acheter un appartement
#! Je veux acheter un mobil home
#! achat mobil home
#! Je souhaite financer un moulin.
u: (<<[financer financez acheter achat payer] _~immobilier>>) ^keep()
	Oui, le prêt immobilier permet de financer l’achat d’un _0, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	^lienSimu()

#! Est-ce que je peux financer le rachat de mon prêt immobilier ?
#! Puis-je emprunter pour racheter mon prêt immobilier ?
#! Est-il possible d'emprunter pour regrouper mes crédits ?
#! Et faire le rachat de l'ensemble de mes prêts existants c'est possible ?
#! Je veux faire racheter mon crédit
#! Je veux racheter mon crédit
#! Peut-on racheter un crédit immo?
#! Est-ce que vous pouvez racheter mon credit ?
u: (!attestation <<[faire financer financez payer emprunter possible veux veut peut pouvez] [rachat racheter regrouper rembourser] ~credit>>) ^keep()
	Bien entendu, le prêt immobilier permet de financer : \n
	• le rachat d’un autre prêt immobilier, \n
	• le regroupement d’au moins deux créances de crédits immobiliers.
	^lienSimu()

#! Que puis-je acheter avec un crédit immobilier ?
#! Qu'est ce que je peux financer avec un crédit immobilier ?
#! Liste moi ce que je peux financer avec un crédit ?
#! Dites moi ce que je peux acheter avec un crédit immobilier ?
#! Quel type de bien peut faire l'objet d'un prêt immo ?
#! A quoi sert le crédit immo ?
#! Qu'est-ce que j'ai le droit d'acheter avec le crédit immo ?
#! Quel peut être l'objet du prêt immo ?
#! Je peux acheter quoi avec un crédit immo?
u: (![attestation rembourser remboursement documents document formalités formalité pièces dossier dossiers] <<[quoi que quel quelle quels quelles liste listez dîtes] [faire acheter financer financez payer objet sert servir]>>) ^keep()
	En général, le crédit immobilier est destiné à l'achat d'un appartement ou d'une maison. \n
	^lienSimu()
	Voulez-vous connaître d'autres types de biens qui peuvent faire objet du prêt immobilier ?
	a: (~oui)
		Le prêt immobilier permet également de financer : \n
		• l’achat d’un terrain destiné à la construction d’un bien immobilier, \n
		• l’achat de parts de sociétés immobilières, \n
		• les travaux de construction d’un bien immobilier, \n
		• pour un emprunteur personne physique : tout type de financement, lorsque le prêt est garanti par une sûreté hypothécaire \( prêt travaux avec une garantie hypothécaire, par exemple \) , \n
		• le rachat d’un prêt immobilier, \n
		• le regroupement d’au moins deux créances de crédits immobiliers. \n
		^gambit(~)
	a: (~non)
		^pick(~compris) ^gambit(~)

#! Puis je acheter à l'étranger
u: (<<[financer financez acheter achat payer offrir] ~etranger>>) ^keep()
	Non, le prêt immobilier est possible seulement pour un bien situé en France métropolitaine. \n
	^gambit(~)

#! Est-ce que je peux acheter un bateau ?
#! Peut-on utiliser le crédit ou prêt pour acheter un avion ?
#! Puis-je m'acheter des animaux de compagnie ?
u: (![action actions titre titres hypothèque fond fonds] [financer financez acheter achat payer offrir] *1) ^keep()
	Non, le prêt immobilier n'est pas destiné à ce type d'achat, voulez-vous connaître les biens qui peuvent faire objet du prêt immobilier ?
	^lienSimu()
	a: (~oui)
		Le prêt immobilier permet de financer : \n
		• l’achat d’un bien immobilier à usage d’habitation ou à usage professionnel et d’habitation, \n
		• l’achat d’un terrain destiné à la construction d’un bien immobilier, \n
		• l’achat de parts de sociétés immobilières, \n
		• les travaux de construction d’un bien immobilier, \n
		• pour un emprunteur personne physique : tout type de financement, lorsque le prêt est garanti par une sûreté hypothécaire \( prêt travaux avec une garantie hypothécaire, par exemple \) , \n
		• le rachat d’un prêt immobilier, \n
		• le regroupement d’au moins deux créances de crédits immobiliers. Le regroupement de crédits peut entraîner un allongement de la durée de remboursement des crédits et majorer le coût total de ceux-ci. \n
		^gambit(~)
	a: (~non)
		^pick(~compris) ^gambit(~)


##### Eligibilité #####

#! Peut on faire un pret a phase
u: (~credit * [phase phases]) ^keep()
	[Post POC] \n
	^gambit(~)

#! Qui peut demander un crédit immobilier ?
#! Est-ce que tout le monde peut bénéficier d'un crédit immobilier ?
#! Est-ce que j'ai le droit à un crédit immobilier ?
#! Est-ce que je suis éligible au prêt immobilier ?
#! quels critère pour avoir un crédit ?
#! Quels sont les critères d'éligibilité au prêt immobilier ?
#! Je veux faire un crdit immobilier ?
u: ELIGI (<<![retour retourner monter dossier déroger] [critères critère peut peux veut veux droit demander bénéficier éligible éligibilité obtenir prétendre] ~credit>>) ^keep()
	Le prêt immobilier est accessible aux clients particuliers majeurs capables et aux clients personnes morales de droit privé, hors financement d'une activité professionnelle \( sous réserve d'acceptation et après étude de votre dossier par BNP Paribas \) . Pour connaitre nos solutions de financement, veuillez cliquer sur les liens suivants : \n
	^"%[Prêt Immo Taux fixe](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-immobilier-taux-fixe)" \n
 	^"%[Prêts relais](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-relais)" \n
	^"%[Prêt Relais Acquisition](https://mabanque.bnpparibas/fr/notre-offre/credits/credits-immobiliers/prets-immobiliers/pret-relais-acquisition)" \n


##### Formalités #####

#! Quels documents il faut pour faire un pret ?
u: (<<[documents document formalités formalité pièces dossier dossiers] [fournir faut besoin nécessaire nécessaires requis demandé demandés]>>) ^keep()
	Pour souscrire un Prêt immobilier à taux fixe, vous devez présenter les pièces justificatives suivantes, et en fournir une copie à la Banque : \n
	• un justificatif lié à l'opération (compromis de vente, attestation notariée ou titre de propriété), \n
	• l'original du dernier avis d'imposition, \n
	• l’échéancier de tous vos prêts en cours si vous en détenez (personnels et immobiliers, chez BNP Paribas et dans les autres banques), \n
	• un justificatif de la demande ou de l'accord des prêts relatifs au financement et obtenus par ailleurs, \n
	• tous autres justificatifs nécessaires à l'étude de votre prêt (en fonction à la fois du bien financé, du type de financement et de l'avancement du projet), \n
	• si vous êtes client dans plusieurs banques, vos 3 derniers relevés de compte. \n
 	\n
	En cas de reprise d'un prêt en cours, vous devez fournir une copie de l'offre de prêt et son plan de remboursement. \n
	^gambit(~)

#! Quelle est la durée maximum d'un pret ? 
u: (<<durée ~credit>>) ^keep()
	Vous pouvez emprunter pour une durée de 2 ans à 30 ans. \n
	^garanties()


##### Garanties #####

#! Combien coute credit logement ?
u: (<<[coûte coût prix] ["crédit logement" garantie garanties hypothèque hypothèques gage gages caution cautions]>>) ^keep()
	Le coût dépend de votre opération immobilière. Accédez à nos simulateurs pour évaluer les différents coûts, notamment les coûts de garantie. \n
	^lienSimu()

#! Quelles sont les garanties exigées pour les crédits immobiliers ?
#! Que sont les gages nécessaires pour un crédit immobilier ?
u: ([que quoi quel quels quelle quelles] * [garantie garanties gage gages caution cautions] * ~credit) ^keep()
	Lors de l’achat d’un bien immobilier, on demande à l’emprunteur une garantie sur le bien financé. Il existe quatre types de garanties, sur le quel souhaitez-vous plus d’informations ? \n
	^garanties()

#! Qu'est-ce qu'une société de cautionnement ?
#! C'est quoi une caution ?
#! Qu'est-ce qu'une participation à un fonds de garantie ?
u: (!SCI [caution cautionnement "fonds de garantie" "crédit logement"]) ^keep()
	Il s'agit d'une personne physique ou morale \( société de caution, mutuelle, etc. \) , qui s’engage à rembourser l’établissement de crédit en cas de défaillance de l’emprunteur. Dans ce cas les frais de garantie pour les particuliers \( pas de frais d’inscription à la conservation des hypothèques \) . La société de caution la plus connue est le Crédit Logement*. \n 
	^"%[*Crédit Logement](http://www.creditlogement.fr/)"
	Désirez-vous des informations plus détaillées sur le cautionnement ? 
	a: (~oui)
		Il s’agit d’une sûreté personnelle : la caution se porte garant de l’emprunteur, en s’engageant à rembourser au prêteur les sommes dues en cas de défaillance. \n
		\n
		La caution peut ensuite se retourner contre l’emprunteur pour demander le remboursement des sommes versées. \n
		• Le cautionnement peut être délivré, sous certaines conditions, par l’organisme de caution professionnelle Crédit Logement avec lequel le prêteur a conclu un partenariat. \n 
		\n
		L’emprunteur doit lui verser une participation à un fonds de garantie \( qui sera éventuellement restituée partiellement en fin de crédit \) ainsi qu’une commission de caution et, le cas échéant, des frais de dossier. \n
		• Il peut s’agir d’une caution personnelle. Dans ce cas, c’est une personne physique qui se porte garante. Elle peut être solidaire, et dans ce cas, le prêteur peut se tourner pour obtenir le remboursement du prêt, à la fois vers l’emprunteur et la caution. \n
		^gambit(~)
	a: (~non)
		^pick(~compris) ^gambit(~)

#! Qu'est ce qu'une garantie hypothécaire ?
#! C'est quoi une hypothèque ?
#! Hypothèque ?
u: ([hypothèque hypothécaire]) ^keep()
	Cette garantie traditionnelle est très répandue. Si l'emprunteur n'honore pas ses remboursements, elle donne droit au créancier de faire saisir le logement et de le vendre aux enchères afin de récupérer le montant des sommes dues. Le plus souvent, elle est utilisée pour des crédits destinés à financer des travaux de construction ou lors de la renégociation d'un prêt. \n
	Désirez-vous des informations plus détaillées ?
	a: (~oui)
		Il s’agit d’une hypothèque conventionnelle de 1er rang. \n
		\n
		Cette garantie est inscrite par acte notarié et publiée à la Conservation des hypothèques. Les frais y afférents sont à la charge de l’emprunteur. \n
		\n
		C'est une sûreté réelle qui confère au prêteur un droit de préférence et un droit de suite. Ainsi, en cas de défaillance, elles lui permet de faire saisir le bien immobilier, d’obtenir sa vente et d’être payé sur le prix de vente par préférence aux autres créanciers. \n
		Elle porte soit sur un bien immobilier dont l’emprunteur est déjà propriétaire, soit sur le bien financé \( y compris pour la partie du prêt qui finance la construction de l’immeuble, les travaux ou une acquisition en état futur d’achèvement \) . L’hypothèque peut aussi être est consentie par un tiers sur un bien qui lui appartient, en garantie du prêt souscrit par l’emprunteur \( en cas de défaillance de celui-ci, le créancier ne peut donc poursuivre ce tiers qu’à hauteur du montant du bien hypothéqué. \) . \n
		\n
		L’hypothèque est prise pour la durée initiale du prêt + 1 an \( mais devient sans objet en cas de remboursement anticipé total \) . L’emprunteur peut, sous conditions, la faire lever. \n
		^gambit(~)
	a: (~non)
		^pick(~compris) ^gambit(~)

#! C'est quoi un un nantissement d'assurance-vie ?
#! Est-ce que je peux mettre en garantie mon assurance-vie  ?
#! Qu'est-ce qu'un nantissement de contrat d'assurance-vie ?
u: ([nantissement garantie gage caution] * [assurance-vie "assurance vie"]) ^keep()
	L’emprunteur peut avoir souscrit un contrat d’assurance-vie auprès d’une compagnie d’assurance, et dispose alors, à ce titre d’une épargne, qu’il peut proposer à la Banque en garantie d’un prêt immobilier. \n
	\n
	Le contrat d’assurance-vie peut être affecté en garantie du prêt immobilier par la signature d’un acte de nantissement. Ce dernier constate le transfert du droit de rachat \( droit pour le souscripteur du contrat d’assurance-vie d’effectuer des retraits, partiel ou total, en cours de vie du contrat \) , au profit de la Banque et organise les droits de la Banque, en qualité de créancier nanti, en cas de défaillance de l’emprunteur ou de décès de l’assuré du contrat d’assurance-vie. \n
	\n
	Tous frais éventuels liés à la constitution ou à la réalisation du nantissement de contrat d’assurance-vie à la charge de l’emprunteur. \n
	\n
	Les principales garanties présentées ci-dessus ne sont pas limitatives. D’autres garanties pourront être prises pour des opérations spécifiques ou complexes \( par exemple nantissement de contrat de capitalisation, ... \) après analyse du dossier par la Banque. \n
	^gambit(~)

#! C'est quoi un nantissement ?
#! Est-ce que je peux mettre en garantie mes actions ?
#! Qu'est-ce qu'un nantissement de compte titres financiers ?
u: ([nantissement <<[garantie gage caution] [actions obligations titres]>>]) ^keep()
	Le nantissement est une alternative à l'hypothèque ou à la caution mutuelle, si vous détenez des valeurs, que vous pouvez remettre au banquier en garantie. Il peut s'agir de contrats d'assurance-vie ou de placements sûrs. L'établissement bancaire a alors la possibilité de vendre ces valeurs pour se rembourser, si l'emprunteur ne paie pas ses échéances. Avez-vous d'autres questions ? \n
	$_tit = ^"Nantissement de titres"
	$_ass = ^"Nantissement d'assurance-vie"
	^btnRP($_tit titres) ^btnRP($_ass assurance)
	a: (titres)
		Il s’agit d’une sûreté réelle qui consiste à affecter en garantie du prêt immobilier consenti par la Banque, un compte de titres financiers ouvert au nom de l’emprunteur ou au nom d’un tiers \( alors dénommé constituant \) sur lequel sont inscrits des sommes et des titres financiers. \n
		\n
		Pendant la durée du nantissement de compte de titres financiers, l’emprunteur/ le constituant ne peut disposer des titres financiers et des sommes figurant sur le compte nanti. \n
		\n
		En cas de défaillance, la Banque doit mettre en demeure l’emprunteur de payer, dans un délai minimum de 8 jours et lui indiquer qu’à défaut de remboursement dans ce délai, elle pourra au choix se faire attribuer la propriété des titres financiers ou procéder à la vente des titres inscrits sur le compte nanti, dans la limite du montant dû. Pendant ce délai, l’emprunteur ou le constituant, peut faire connaître à la Banque l’ordre dans lequel les sommes et titres financiers devront être attribués ou vendus. \n
		^gambit(~)
	a: (assurance)
		L’emprunteur peut avoir souscrit un contrat d’assurance-vie auprès d’une compagnie d’assurance, et dispose alors, à ce titre d’une épargne, qu’il peut proposer à la Banque en garantie d’un prêt immobilier. \n
		\n
		Le contrat d’assurance-vie peut être affecté en garantie du prêt immobilier par la signature d’un acte de nantissement. Ce dernier constate le transfert du droit de rachat \( droit pour le souscripteur du contrat d’assurance-vie d’effectuer des retraits, partiel ou total, en cours de vie du contrat \) , au profit de la Banque et organise les droits de la Banque, en qualité de créancier nanti, en cas de défaillance de l’emprunteur ou de décès de l’assuré du contrat d’assurance-vie. \n
		\n
		Les principales garanties présentées ci-dessus ne sont pas limitatives. D’autres garanties pourront être prises pour des opérations spécifiques ou complexes \( par exemple nantissement de contrat de capitalisation, ... \) après analyse du dossier par la Banque. \n
		^gambit(~)

#! C'est quoi le privilège de prêteur de deniers ?
u: PPD (prêteur de [denier deniers]) ^keep()
	Il s’agit d'une garantie traditionnelle portant sur les biens existants \( de l’ancien ou du neuf dont la construction est complètement terminée ou des terrains \) . Elle est moins chère que l’hypothèque \( pas de frais d’enregistrement et pas de taxe de publicité foncière \) . \n
	^gambit(~)

#! C'est quoi le PPD ?
u: (PPD) ^keep()
	^reuse(PPD)

#! C'est quoi une garantie réelle ?
u: ([garantie garanties] [réelle réelles]) ^keep()
	Si vous souhaitez bénéficier d'un prêt, la banque demandera généralement des garanties. En cas de problèmes, ce dispositif permet aux établissements prêteurs d'appréhender et de faire vendre le bien financé pour récupérer les fonds octroyés. \n
	On distingue trois types de garanties réelles :
	$_lib = ^"prêteur de deniers"
	^btn(reply hypothèque) ^btnRP($_lib PPD) ^btn(reply nantissement)

#! C'est quoi une sûreté réelle ?
u: ([sûreté sûretés sureté suretés] [réelle réelles]) ^keep()
	Si vous souhaitez bénéficier d'un prêt, la banque demandera généralement des sûretés. En cas de problèmes, ce dispositif permet aux établissements prêteurs d'appréhender et de faire vendre le bien financé pour récupérer les fonds octroyés. \n
	On distingue trois types de sûretés réelles :
	$_lib = ^"prêteur de deniers"
	^btn(reply hypothèque) ^btnRP($_lib PPD) ^btn(reply nantissement)

#! Une garantie ?
u: (![montant frais] [garantie garanties]) ^keep()
	Si vous souhaitez bénéficier d'un prêt, la banque demandera généralement des garanties. En cas de problèmes, ce dispositif juridique permet aux établissements prêteurs d'appréhender et de faire vendre le bien financé pour récupérer les fonds octroyés. \n
	On distingue quatre types de garanties : ^garanties()


##### Simulateur : Taux / Tarif / remboursement / frais #####

#! Combien puis-je emprunter ?
#! Tarif assurance ?
#! Quel tarif ?
#! Quel remboursement ?
#! remboursement ?
#! Quel taux proposez-vous ?
#! Quel taux nominal annuel ?
#! Quel taux d'assurance ou assurance ?
#! Taux ?
#! Coût assurance ?
#! Coût total du prêt ?
#! Quel coût du prêt ?
#! Montant des frais de garanties ?
#! Montant des garanties ?
#! Montant assurance ?
#! Montant du prêt ?
#! Quel frais de dossier ?
#! Frais de dossier ?
#! Montant des frais de dossier ?
#! Quel frais de garantie ?
#! Frais de garantie ?
u: ([taux tarif remboursement <<combien [emprunt emprunter]>> <<montant [garanties garantie assurance ~credit]>> <<coût [assurance ~credit]>> <<frais [dossier garantie]>>]) ^keep()
	Accédez à notre simulateur pour connaitre le montant que vous pouvez emprunter, vers l'onglet "montant à emprunter" 
	^lienSimu()



########## PAS TOUT COMPRIS ##########

u: (_~habitatm) ^keep()
	Souhaitez-vous un prêt immobilier pour acheter un _0 ?
	a: (~oui)
		Cela est possible, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	a: (~non)
		^reuse(ECOUTE)

u: (_~habitatf) ^keep()
	Souhaitez-vous un prêt immobilier pour acheter une _0 ?
	a: (~oui)
		Cela est possible, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	a: (~non)
		^reuse(ECOUTE)

u: (_~habitatp) ^keep()
	Souhaitez-vous un prêt immobilier pour acheter des _0 ?
	a: (~oui)
		Cela est possible, à condition que le bien soit destiné à usage d’habitation ou à usage professionnel et d’habitation.
	a: (~non)
		^reuse(ECOUTE)

u: (_~immobilier) ^keep()
	Souhaitez-vous un prêt immobilier pour acheter un _0 ?
	a: (~oui)
		Il est effectivement possible de financer l'achat d'un _0 avec un prêt immobilier.
	a: (~non)
		^reuse(ECOUTE)



########## AUTRE ##########

s: ([pas plus] de {[autre autres]} [question questions]) ^keep()
	^reuse(~fin.DISPO)

s: ([je cela] [ai est] fini) ^keep()
	^reuse(~fin.DISPO)

#! crédit immo
#! pret immo
u: (< _~credit > $lastpascompris!=true ) ^keep()
	$pascompris = true
	Que voulez-vous savoir a propos du _0 ?\n
	^reuse(ELIGI)

#! Où en est mon dossier ?
u: ([<<[pièce pièces] fournir>> <<combien emprunter>> dossier devis ~credit]) ^keep()
	Je n'ai pas suffisament d'informations sur votre dossier pour répondre à votre question.

u: ([pas rien] compris) ^keep()
	J'en suis désolé, je tâcherai de m'améliorer.
	^gambit(~)

u: (< ~bonjour {Laure} >) ^keep()
	^reuse(ECOUTE)

?: (["ça va" "comment allez"]) ^keep()
	Très bien, merci. ^reuse(AIDER)

#! lol
u: (< *1 >) ^keep()
	Je peux répondre à des questions un peu plus élaborées, que désirez-vous savoir à ce sujet ?

u: ($lastpascompris==true ) ^keep()
	$pascompris = true
	Je n’ai pas compris votre question, je suis encore en développement. Si vous avez besoin d'aide dites "aide".

u: () ^keep()
	$pascompris = true
	Je n’ai pas compris votre question, pouvez-vous reformuler ?
	